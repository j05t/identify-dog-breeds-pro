// set to 'armeabi-v7a', 'all', 'arm64-v8a'
// armeabi-v7a will only build for armeabi-v7a (most smartphones)
// all will build for arm64-v8a, armeabi-v7a, x86, x86_64
def buildFor = 'armeabi-v7a'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies { classpath 'com.android.tools.build:gradle:8.3.2' }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

project.ext.ASSET_DIR = projectDir.toString() + '/assets'

apply plugin: 'com.android.application'

android {
    namespace = 'com.jstappdev.identify_dog_breeds_pro'
    compileSdk 34
    defaultConfig {
        applicationId = 'com.jstappdev.identify_dog_breeds_pro'
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 68
        versionName "28"
    }

    if (buildFor == 'armeabi-v7a') {
        defaultConfig {
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }
    } else if (buildFor == 'arm64-v8a') {
        defaultConfig {
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
    }

    sourceSets {
        main {
            java {
                srcDir 'src'
            }

            manifest.srcFile 'AndroidManifest.xml'
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = [project.ext.ASSET_DIR]
            jniLibs.srcDirs = ['libs']
        }

        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    lint {
        abortOnError false
        disable 'MissingTranslation'
    }
    buildFeatures {
        renderScript true
        aidl true
    }
}

dependencies {
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))
    implementation 'org.tensorflow:tensorflow-android:1.13.1'
    implementation 'androidx.activity:activity:1.8.2'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.fragment:fragment:1.6.2'
}